plugins {
  id "idea"
  id "com.github.mxenabled.coppuccino" version "6.+" apply false
  id "com.github.mxenabled.vogue" version "3.+"
  id "io.freefair.lombok" version "8.14.3" apply false
  id "io.github.gradle-nexus.publish-plugin" version "1.1.+"
}

version "6.0.0" // x-release-please-version

def platformProject = "platform"
def publishedProjects = [
  platformProject,
  "common",
  "context",
  "gateway",
  "gateway-generator",
  "http",
  "messaging",
  "testing",
  "utilities"
]

allprojects {
  if (it.name != platformProject) {
    apply plugin: "java"
  }

  group "com.mx.path-core"
  description "MX Path Core"
  version rootProject.version

  java {
    toolchain {
      languageVersion = JavaLanguageVersion.of(21)
    }
  }

  repositories {
    mavenCentral()
    mavenLocal()
  }

  configurations.all {
    //remove after upgrading checkstyle 13.0.0
    resolutionStrategy.eachDependency { details ->
      if (details.requested.group == "org.apache.commons" && details.requested.name == "commons-lang3") {
        details.useVersion "3.18.0"
      }
    }
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
  }
}

subprojects {
  if (it.name != platformProject) {
    apply plugin: "java-library"
    apply plugin: "groovy"
    apply plugin: "com.github.mxenabled.coppuccino"
    apply plugin: "com.github.mxenabled.vogue"
    apply plugin: "io.freefair.lombok"

    ext {
      mockitoVersion = "5.21.0"
      spockVersion = "2.4-M6-groovy-3.0"
      junitVersion = "5.14.0"
    }

    dependencies {
      constraints {
        api("com.google.code.gson:gson") {
          version {
            require "[2.13.0,13.0.0)"
          }
        }
        api("io.opentracing:opentracing-api") {
          version {
            require "[0.33,0.34)"
          }
        }
        api("io.opentracing:opentracing-noop") {
          version {
            require "[0.33,0.34)"
          }
        }
        api("io.opentracing:opentracing-util") {
          version {
            require "[0.33,0.34)"
          }
        }
        api("com.google.guava:guava") {
          version {
              require "[32.0,33.0)"
          }
        }
        api("com.github.rholder:guava-retrying") {
          version {
            require "[2.0,3.0)"
          }
        }
        api "com.datadoghq:dd-trace-api:1.38.0"
        api "org.slf4j:slf4j-api:1.7.30"
        api "org.apache.httpcomponents:httpclient:4.5.13"

        // -----------------------------------------------------------------
        // These re-add JAXB classes needed for SOAP interaction.
        // Version changes to any of these need to be tested (not just built)
        //   with an apps that use SOAP.
        // -----------------------------------------------------------------
        api "jakarta.xml.bind:jakarta.xml.bind-api:4.0.4!!"
        api "jakarta.xml.soap:jakarta.xml.soap-api:3.0.2!!"
        api "com.sun.xml.bind:jaxb-impl:4.0.6!!"
        api "org.glassfish.jaxb:jaxb-runtime:4.0.6!!"
        // -----------------------------------------------------------------

      }

      testImplementation "org.mockito:mockito-core:${project.ext.mockitoVersion}"
      testImplementation "org.spockframework:spock-core:${project.ext.spockVersion}"
      testImplementation "org.junit.jupiter:junit-jupiter-api:${project.ext.junitVersion}"
    }

    test {
      useJUnitPlatform()
      // restore reflection on base java classes to fix issue w/ tests
      jvmArgs "--add-opens", "java.base/java.lang=ALL-UNNAMED"
    }

    compileJava { options.compilerArgs << "-parameters" }

    javadoc {
      classpath = configurations.compileClasspath
      options {
        setMemberLevel JavadocMemberLevel.PUBLIC
        setAuthor true
      }
    }

    coppuccino {
      rootDir = "${projectDir}/"
    }

    vogue {
      dependencyUpdatesOutputDir = "${projectDir}/build/dependencyUpdates"
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
      archiveClassifier = "sources"
      from sourceSets.main.allSource
    }

    task packageJavadoc(type: Jar) {
      archiveClassifier = "javadoc"
      from javadoc
    }

    artifacts {
      archives sourcesJar
      archives jar
      archives packageJavadoc
    }

    if (publishedProjects.contains(it.name)) {
      apply plugin: "maven-publish"
      apply plugin: "signing"

      publishing {
        publications {
          maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact packageJavadoc

            pom {
              groupId = project.group
              artifactId = project.name
              name = project.name
              description = project.description
              url = "https://github.com/mxenabled/path-core"

              developers {
                developer {
                  name = "MX"
                  email = "path@mx.com"
                  organization = "MX Technologies Inc."
                  url = "http://www.mx.com"
                }
              }

              licenses {
                license {
                  name = "Proprietary"
                  url = "https://github.com/mxenabled/path-core/blob/master/LICENSE"
                  distribution = "repo"
                }
              }

              scm {
                connection = "scm:git:git@github.com:mxenabled/path-core.git"
                url = "https://github.com/mxenabled/path-core/tree/master"
              }
            }
          }
        }
      }

      signing {
        def signingKey = findProperty("signingKey")
        def signingPassword = findProperty("signingKeyPassword")
        if (signingKey != null && signingKey != "") {
          useInMemoryPgpKeys(signingKey, signingPassword)
          sign publishing.publications.maven
          logger.lifecycle("Configuring signing for ${project.name}")
        } else {
          logger.lifecycle("Skipping artifact signing for ${project.name} - missing signing key")
        }
      }
    }
  }
}

nexusPublishing {
  def ossrhUserName = findProperty("ossrhUserName")
  def ossrhToken = findProperty("ossrhToken")

  repositories {
    central {
      nexusUrl = uri("https://ossrh-staging-api.central.sonatype.com/service/local/")
      snapshotRepositoryUrl = uri("https://central.sonatype.com/repository/maven-snapshots/")
      username = ossrhUserName
      password = ossrhToken
    }
  }
}

task spotlessApply {
  subprojects.each {
    if (it.name != platformProject) {
      it.afterEvaluate {
        def spotlessApplyTask = it.tasks.findByName("spotlessApply")
        dependsOn(spotlessApplyTask)
      }
    }
  }
}

sourceSets {
  main {
    java { srcDir "build/generated/sources/annotationProcessor" }
  }
}

task subdependencies {
  subprojects.each {
    if (it.name != platformProject) {
      it.afterEvaluate {
        def dependenciesTask = it.tasks.findByName("dependencies")
        dependsOn(dependenciesTask)
      }
    }
  }
}

project.tasks.getByPath("dependencies").finalizedBy("subdependencies")

wrapper {
  gradleVersion = "8.14.3"
  distributionType = Wrapper.DistributionType.ALL
}
